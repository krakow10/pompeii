// Implementatin of a basic incompressible, homogeneous fluid simulation. See https://www.dgp.toronto.edu/public_user/stam/reality/Research/pdf/ns.pdf, https://developer.nvidia.com/gpugems/gpugems/part-vi-beyond-triangles/chapter-38-fast-fluid-dynamics-simulation-gpu, https://github.com/PavelDoGreat/WebGL-Fluid-Simulation/tree/master.
#version 460
#extension GL_EXT_buffer_reference : require
#extension GL_EXT_scalar_block_layout : require
#extension GL_GOOGLE_include_directive : require

layout(local_size_x = 8, local_size_y = 8) in;

layout(set = 0, binding = 0, rg32f) uniform image2D input_velocity;
layout(set = 0, binding = 1, r32f) readonly uniform image2D curl_texture;

#include "shared.glsl"

// Use the `curl` field to update the velocity field. The goal is to increase the vorticity of the fluid.
void main() {
  const ivec2 coords = ivec2(gl_GlobalInvocationID.xy);
  if(coords.x >= push_constants.screen_size.x || coords.y >= push_constants.screen_size.y) {
    return;
  }
  vec2 velocity = imageLoad(input_velocity, coords).xy;

  const float curl_left = imageLoad(curl_texture, coords - ivec2(1, 0)).x;
  const float curl_right = imageLoad(curl_texture, coords + ivec2(1, 0)).x;
  const float curl_top = imageLoad(curl_texture, coords + ivec2(0, 1)).x;
  const float curl_bottom = imageLoad(curl_texture, coords - ivec2(0, 1)).x;
  const float curl_center = imageLoad(curl_texture, coords).x;

  vec2 adjusting_force = vec2(abs(curl_top) - abs(curl_bottom), abs(curl_left) - abs(curl_right));
  adjusting_force /= length(adjusting_force) + 0.0001;
  adjusting_force *= push_constants.vorticity_strength * curl_center;

  velocity += adjusting_force * push_constants.delta_time;
  imageStore(input_velocity, coords, vec4(velocity, 0, 0));
}
